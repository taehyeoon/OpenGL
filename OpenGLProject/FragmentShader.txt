#version 400 core

in vec2 texCoord;
in vec3 normal;
in vec3 fragPos;
out vec4 color;

// Kernel
uniform mat3 u_kernel;
uniform float u_kernel_sum;
uniform bool u_isNegative;

// Lighting
uniform vec3 u_lightColor;
uniform vec3 u_lightPos;
uniform vec3 u_viewPos;

uniform sampler2D texture1;

void main()
{
    vec4 objectColor;

    vec2 delta = 1.0 / textureSize(texture1, 0);
    
    vec4 coord_l_u = texture(texture1, texCoord + (delta * vec2(-1.0,  1.0))) * u_kernel[0][0]; // left, up
    vec4 coord_c_u = texture(texture1, texCoord + (delta * vec2( 0.0,  1.0))) * u_kernel[1][0]; // center, up
    vec4 coord_r_u = texture(texture1, texCoord + (delta * vec2( 1.0,  1.0))) * u_kernel[2][0]; // right, up
    
    vec4 coord_l_c = texture(texture1, texCoord + (delta * vec2(-1.0,  0.0))) * u_kernel[0][1]; // left, center
    vec4 coord_c_c = texture(texture1, texCoord + (delta * vec2( 0.0,  0.0))) * u_kernel[1][1]; // center, center
    vec4 coord_r_c = texture(texture1, texCoord + (delta * vec2( 1.0,  0.0))) * u_kernel[2][1]; // right, center
    
    vec4 coord_l_d = texture(texture1, texCoord + (delta * vec2(-1.0, -1.0))) * u_kernel[0][2]; // left, down
    vec4 coord_c_d = texture(texture1, texCoord + (delta * vec2( 0.0, -1.0))) * u_kernel[1][2]; // center, down
    vec4 coord_r_d = texture(texture1, texCoord + (delta * vec2( 1.0, -1.0))) * u_kernel[2][2]; // right, down
    
    objectColor = (coord_l_u + coord_c_u + coord_r_u +
                    coord_l_c + coord_c_c + coord_r_c +
                    coord_l_d + coord_c_d + coord_r_d) / u_kernel_sum;

    // Set cube color red
    // objectColor = vec4(1,0,0,1);
    
    // Ambient
    float ambientStrength = 0.1;
    vec3 ambient = ambientStrength * u_lightColor;
    
    // Diffuse
    vec3 norm = normalize(normal);
    vec3 lightDir = normalize(u_lightPos - fragPos);
    float diff = max(dot(norm, lightDir), 0.0);
    vec3 diffuse = diff * u_lightColor;
    
    // Specular
    float specularStrength = 0.5;
    vec3 viewDir = normalize(u_viewPos - fragPos);
    vec3 reflectDir = reflect(-lightDir, norm);
    float spec = pow(max(dot(viewDir, reflectDir), 0.0), 256);
    vec3 specular = specularStrength * spec * u_lightColor;

    color = vec4(ambient + diffuse + specular, 1.0) * objectColor;

    if(u_isNegative)
        color = vec4(1.0) - color;
        
    color.a = 1.0;
}
